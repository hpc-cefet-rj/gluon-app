buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.0.8'
    }
}

apply plugin: 'scala'
apply plugin: 'org.javafxports.jfxmobile'

repositories {
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
}

mainClassName = 'com.gluonhq.sqlite.GluonSQLite'

dependencies {
    compile 'org.scala-lang:scala-library:2.11.7'

    compile 'com.gluonhq:charm:2.2.0'

    desktopRuntime 'com.gluonhq:charm-desktop:2.2.0'
    // Desktop SQL -> https://github.com/xerial/sqlite-jdbc
    desktopRuntime 'org.xerial:sqlite-jdbc:3.8.11.2'

    embeddedRuntime 'com.gluonhq:charm-desktop:2.2.0'
    // Embedded SQL -> https://github.com/xerial/sqlite-jdbc
    embeddedRuntime 'org.xerial:sqlite-jdbc:3.7.2'

    androidRuntime "com.gluonhq:charm-android:2.2.0"
    // Android SQL -> https://github.com/SQLDroid/SQLDroid
    androidRuntime 'org.sqldroid:sqldroid:1.0.3'

    iosRuntime "com.gluonhq:charm-ios:2.2.0"
    // ios SQL -> https://github.com/robovm/robovm 1.8
}

jfxmobile {
    android {
        manifest = 'src/android/AndroidManifest.xml'
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.gluonhq.**.*',
                'io.datafx.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*',
                'SQLite.**.*'
        ]
    }
    embedded {
        remotePlatforms {
            raspberry {
                host = '192.168.1.51'
                username = 'pi'
                password = 'pi'
                workingDir = '/home/pi/Gluon'
                jreLocation = '/usr/lib/jvm/jdk-8-oracle-arm32-vfp-hflt'
                execPrefix = 'sudo'
            }
        }
    }
}
